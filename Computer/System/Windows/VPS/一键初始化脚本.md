# 一键初始化脚本

```powershell
@echo off
cls
SETLOCAL ENABLEDELAYEDEXPANSION
rem 检查参数是否满足4个
if %4 EQU "" (
echo parameter error
goto batexit 
)

echo --  S/Get Basic Info  --
set pppoename=宽带连接
for /F %%a in ('cmd /c reg query "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\Connections" ') do (
SET b=%%a
SET j=!b:~0,4!
if "!j!" NEQ "Defa" (
if "!j!" NEQ "Save" (
set regaccount=%%a
)
)
)
if "%regaccount%" NEQ ""  (
if "%regaccount:~0,4%" NEQ "HKEY" (
set pppoename=!regaccount!
)
)

rem 变量设置
set smbfolder=%1
set downloadlink=%2
set proxyhostname=%3
set account=%4
set password=%5
set adminpass=zhiweidata
set proxyfile=windows-proxy-init
set proxyfileext=%proxyfile%.rar
set desktop=%userprofile%\desktop
set deskclientfolder=%desktop%\client
set deskinitfolder=%desktop%\%proxyfile%

rem 变量呼出
echo Download Link: %downloadlink%
echo Default Proxy File: %proxyfileext%
echo Proxy Hostname: %proxyhostname%
echo PPPOE Name: %pppoename%
echo PPPOE Account: %account%
echo PPPOE Password: %password%

echo.
echo ------  PPPOE  ------
:pppoeLink
ping 1.2.4.8 -n 1 >nul
if %errorlevel% EQU 1 (
rasdial %pppoename% %account% %password%
goto pppoeLink
) else (
echo Connected Internet.
)

rem 如果已经下载文件
if EXIST "%desktop%\%proxyfileext%" (
goto rar
)

echo.
echo -- downloading %downloadlink% --
cd %desktop%
\\%smbfolder%\%proxyfile%\download.vbs %downloadlink% %desktop%\%proxyfileext%
echo \\%smbfolder%\%proxyfile%\download.vbs %downloadlink% %proxyfileext%
if NOT EXIST "%desktop%\%proxyfileext%" (
echo vbs download file is fail.Let's using IE to download
explorer %downloadlink%
echo please continue when download complete.
pause
)

:rar
echo.
echo ----  unrar file %proxyfileext% ----
rem 使用本地rar命令程序
if EXIST "C:\Program Files\WinRAR\Rar.exe" (
    echo auto raring
    "C:\Program Files\WinRAR\Rar.exe" x -t -o-p %desktop%\%proxyfileext% %desktop%
rem 使用远程rar命令程序
) else (
   \\%smbfolder%\%proxyfile%\rar.exe x -t -o-p %desktop%\%proxyfileext% %desktop%
)

echo.
echo ----  ready init client ----

echo move client to desktop
move /Y %deskinitfolder%\client %desktop%\

echo move javato ProgramFile
move /Y %deskinitfolder%\Java "%ProgramFiles%"

echo init group poilcy
regedit /s %deskinitfolder%\init_grouppoilcy.reg

echo init java system path
regedit /s %deskinitfolder%\init_javapath.reg

echo init tcp set
regedit /s %deskinitfolder%\init_tcp.reg

echo move new bat to client folder
move /Y %deskinitfolder%\*.bat %deskclientfolder%
rem 覆盖下载包中的kill_start.bat
copy /Y \\%smbfolder%\%proxyfile%\kill_start.bat %deskclientfolder%

rem 手动生成start.bat
echo start.bat:
echo C:\Users\Administrator\Desktop\client\run.bat windows-%proxyhostname% %pppoename% %account% %password%
echo @C:\Users\Administrator\Desktop\client\run.bat windows-%proxyhostname% %pppoename% %account% %password% >>%deskclientfolder%\start.bat

if NOT EXIST "%deskclientfolder%\start.bat" (
echo ERROR: start.bat is not exist
)
if NOT EXIST "%deskclientfolder%\run.bat" (
copy /Y \\%smbfolder%\%proxyfile%\run.bat %deskclientfolder%
)
echo.
echo ----  init will over  ----
rmdir /q /s %deskinitfolder%
erase %desktop%\%proxyfileext%
echo mod adminsitrator'password to %adminpass%
net user Administrator %adminpass%
rem 关闭防火墙
sc config mpssvc start= disabled
echo system will reboot at 0s later
shutdown -r -t 0
msg * everthing is ok!
exit /b 0
:batexit
exit /b 1
```



